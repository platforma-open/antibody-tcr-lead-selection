// Template for clonotype filtering and sampling
self := import("@platforma-sdk/workflow-tengo:tpl")
exec := import("@platforma-sdk/workflow-tengo:exec")
assets := import("@platforma-sdk/workflow-tengo:assets")
pframes := import("@platforma-sdk/workflow-tengo:pframes")
xsv := import("@platforma-sdk/workflow-tengo:pframes.xsv")
pSpec := import("@platforma-sdk/workflow-tengo:pframes.spec")
render := import("@platforma-sdk/workflow-tengo:render")
sampledColsConv := import(":sampled-cols-conv")
sampledExportConv := import(":sampled-export-conv")
json := import("json")

self.defineOutputs("sampledRows", "finalClonotypes", "sampledColumnsExport")

self.body(func(inputs) {

    cloneTable := inputs.cloneTable
    datasetSpec := inputs.datasetSpec
    filterMap := inputs.filterMap
    rankingMap := inputs.rankingMap
    topClonotypes := inputs.topClonotypes

    outputs := {}
    finalClonotypes := undefined
    
    // Run filtering script
    filterResult := exec.builder().
        software(assets.importSoftware("@platforma-open/milaboratories.top-antibodies.sample-clonotypes:filter")).
        mem("16GiB").
        cpu(1).
        addFile("clonotypes.parquet", cloneTable).
        arg("--parquet").arg("clonotypes.parquet").
        arg("--out").arg("filteredClonotypes.parquet").
        arg("--filter-map").arg(string(json.encode(filterMap))).
        saveFile("filteredClonotypes.parquet").
        printErrStreamToStdout().
        cache(24 * 60 * 60 * 1000).
        run()

    // Save filtered parquet file
    filteredClonotypes := filterResult.getFile("filteredClonotypes.parquet")

    // Check if In Vivo Score is in the ranking map
    hasInVivoScore := false
    for key, _ in rankingMap {
        if key == "inVivoScore" {
            hasInVivoScore = true
        }
    }

    // Store outputs
    sampledColsParams := sampledColsConv.getColumns(datasetSpec, false, false) // No ranking column
    filteredClonotypesPf := xsv.importFile(filteredClonotypes, "parquet", sampledColsParams,
                                        {cpu: 1, mem: "16GiB"})

    // Prepare outputs in case there is no top ranking
    outputs["sampledRows"] = pframes.exportFrame(filteredClonotypesPf)
    finalClonotypes = filteredClonotypes

	if topClonotypes != undefined {

		////////// Top Clonotypes Sampling //////////
		// Run sampling script on filtered data
		builder := exec.builder().
			software(assets.importSoftware("@platforma-open/milaboratories.top-antibodies.sample-clonotypes:main")).
			mem("16GiB").
			cpu(1).
			addFile("filteredClonotypes.parquet", filteredClonotypes).
			arg("--parquet").arg("filteredClonotypes.parquet").
			arg("--n").arg(string(topClonotypes)).
			arg("--ranking-map").arg(string(json.encode(rankingMap)))

		// Add diversification column if provided
		if inputs.diversificationColumn != undefined && inputs.diversificationColumn != "" {
			builder = builder.arg("--diversification-column").arg(inputs.diversificationColumn)
		}

		sampleClones := builder.
			arg("--out").arg("sampledClonotypes_top.parquet").
			saveFile("sampledClonotypes_top.parquet").
			printErrStreamToStdout().
			cache(24 * 60 * 60 * 1000).
			run()

		// Save top clonotypes parquet file
		finalClonotypes = sampleClones.getFile("sampledClonotypes_top.parquet")
		
		// Store outputs
        sampledColsParams := sampledColsConv.getColumns(datasetSpec, true, hasInVivoScore) // Add ranking column
		sampledColumnsPf := xsv.importFile(finalClonotypes, "parquet", sampledColsParams,
											{cpu: 1, mem: "16GiB"})
		outputs["sampledRows"] = pframes.exportFrame(sampledColumnsPf)
	} 

    outputs["finalClonotypes"] = finalClonotypes

    // Export only the sampling column to be used as a filter downstream
    sampledExportsParams := sampledExportConv.getColumns(datasetSpec, filterMap,
        rankingMap, inputs.diversificationColumn, topClonotypes)
    sampledColumnsOnlyPf := xsv.importFile(finalClonotypes, "parquet", sampledExportsParams,
                                        {cpu: 1, mem: "16GiB"})

    // Create trace with filter descriptions and inject into exported column
    traceLabel := inputs.filterTraceLabel
    if is_undefined(traceLabel) || traceLabel == "" {
        traceLabel = "Selected Leads"
    }
    trace := pSpec.makeTrace(datasetSpec,
        {
            type: "milaboratories.antibody-tcr-lead-selection",
            importance: 30,
            label: traceLabel
        })
    exportPf := pframes.pFrameBuilder()
    exportPf.add("link", trace.inject(sampledColumnsOnlyPf["link.spec"]), sampledColumnsOnlyPf["link.data"])
    outputs["sampledColumnsExport"] = exportPf.build()

    return outputs
}) 